
#include <Arduino.h>
#include "HoverboardAPI.h"

HoverboardAPI hoverboard;
String serialBuffer = "";

// Inicjacja
void setup() {
  Serial.begin(115200);       // połączenie z PC
  Serial1.begin(115200);      // połączenie z hoverboardem (UART)
  hoverboard.begin(Serial1);  // inicjalizacja API

  Serial.println("Hoverboard Serial Bridge ready");
}

// Funkcje
long mapSpeedToHB(float value) {
  long speed = (long)(value * 100.0);
  return constrain(speed, -100, 100);
}

void setLeftRight(float left, float right) {
  long l = mapSpeedToHB(left);
  long r = mapSpeedToHB(right);

  hoverboard.setSpeed(l, r);  // wysyłanie do hoverboarda

  Serial.print("L="); Serial.print(l);
  Serial.print(" R="); Serial.println(r);
}

void stopAll() {
  setLeftRight(0.0, 0.0);
}

// komendy

void processCommand(const String &cmd) {
  if (cmd.length() == 0) return;

  char buffer[cmd.length() + 1];
  cmd.toCharArray(buffer, sizeof(buffer));

  char *token = strtok(buffer, " ");
  if (!token) return;

  if (strcmp(token, "S") == 0) {
    token = strtok(NULL, " ");
    if (!token) return;

    if (strcmp(token, "LR") == 0) {
      char *p1 = strtok(NULL, " ");
      char *p2 = strtok(NULL, " ");
      if (p1 && p2) setLeftRight(atof(p1), atof(p2));
    }
  } 
  else if (strcmp(token, "STOP") == 0) {
    stopAll();
  } 
  else if (strcmp(token, "PING") == 0) {
    Serial.println("PONG");
  }
}

// pętla 
void loop() {
  while (Serial.available()) {
    char c = Serial.read();
    if (c == '\n' || c == '\r') {
      if (serialBuffer.length() > 0) {
        processCommand(serialBuffer);
        serialBuffer = "";
      }
    } else {
      serialBuffer += c;
    }
  }

  hoverboard.loop();
}
